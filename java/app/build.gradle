buildscript {
  ext.protobufPlugInVersion = '0.9.4'
  ext.protobufVersion = '3.21.7'
  ext.jerseyVersion = '3.1.0'
  ext.junitVersion = '5.9.0'
  ext.mockitoVersion = '5.2.0'
  ext.postgresVersion = '42.5.1'
  ext.jooqVersion = '3.17.7'
  ext.guiceVersion = '5.1.0'
}

plugins {
  id 'java'
  id 'com.google.protobuf' version "${protobufPlugInVersion}"
  id 'war'
  id 'idea'
  id 'nu.studer.jooq' version '8.0'
  id "io.sentry.jvm.gradle" version "4.14.1"
}

repositories {
  mavenCentral()
}

sourceSets {
  main {
    java {
      srcDirs += './src/main/generated/proto'
      srcDirs += './src/main/generated/jooq'
      srcDirs -= './build/generated'
    }
    proto {
      srcDir '../../proto'
    }
  }
}

sentry {
  // Generates a JVM (Java, Kotlin, etc.) source bundle and uploads your source code to Sentry.
  // This enables source context, allowing you to see your source
  // code as part of your stack traces in Sentry.
  includeSourceContext = true

  org = "getalby"
  projectName = "albyhub-vss"
  authToken = System.getenv("SENTRY_AUTH_TOKEN")
}

group 'org.vss'
version '1.0'

war {
  archiveFileName = "vss-${project.version}.war"
}

dependencies {
  implementation "com.google.protobuf:protobuf-java:$protobufVersion"

  implementation('org.glassfish.jersey.containers:jersey-container-servlet:3.1.0')

  //jOOQ & Postgres impl deps
  implementation "org.jooq:jooq:$jooqVersion"
  implementation "org.jooq:jooq-meta:$jooqVersion"
  implementation "org.jooq:jooq-codegen:$jooqVersion"
  runtimeOnly "org.postgresql:postgresql:$postgresVersion"
  jooqGenerator "org.postgresql:postgresql:$postgresVersion"
  implementation 'com.zaxxer:HikariCP:5.0.1' // Connection pooling for postgres/jdbc

  implementation "com.google.inject:guice:$guiceVersion"
  implementation('org.glassfish.jersey.inject:jersey-hk2:3.1.0')
  implementation "org.glassfish.hk2:guice-bridge:3.0.3"

  implementation "com.auth0:java-jwt:4.2.0"

  compileOnly 'org.projectlombok:lombok:1.18.24'
  annotationProcessor 'org.projectlombok:lombok:1.18.24'

  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testImplementation "org.mockito:mockito-core:$mockitoVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
  testImplementation "org.hamcrest:hamcrest-library:2.2"
  testImplementation "org.testcontainers:junit-jupiter:1.17.6"
  testImplementation "org.testcontainers:postgresql:1.17.6"
}

test {
  useJUnitPlatform()
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }

  generatedFilesBaseDir = "${projectDir}/src/"
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          outputSubDir = 'generated/proto'
        }
      }
    }
  }
}

jooq {
  configurations {
    main {
      generateSchemaSourceOnCompilation = false

      generationTool {
        jdbc {
          driver = 'org.postgresql.Driver'
          url = 'jdbc:postgresql://localhost:5432/postgres'
          user = 'postgres'
          password = 'YOU_MUST_CHANGE_THIS_PASSWORD'
          properties {
            property {
              key = 'ssl'
              value = 'false'
            }
          }
        }
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.postgres.PostgresDatabase'
            inputSchema = 'public'
          }
          generate {
            deprecated = false
            records = true
            immutablePojos = true
            fluentSetters = true
          }
          target {
            packageName = 'org.vss.postgres'
            directory = 'src/main/generated/jooq'
          }
          strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
        }
      }
    }
  }
}
